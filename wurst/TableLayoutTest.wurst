package TableLayoutTest
import ClosureTimers
import TableLayout
import Icons
import GameTimer
import Textures
import MultiboardAttach
import FramehandleNames



init

    doAfter(.1) ->

        let bar1 = new UIBar(0.075, 0.01)

        doPeriodically(0.01) cb ->
            bar1.setValue((1 + currentTime.sin()) / 2.)

        let checkbox = new UICheckbox(0.025, 0.025, true)

        let baseFrame = defaultFrame()

        let nestedTable1 = new TableLayout(0.1, 0.05)
        ..row()..center()
        ..add(img(Icons.bTNAcolyte))
        ..row()..center()
        ..add(p("Acolyte"))..padTop(0.01)

        let nestedTable2 = new TableLayout(0.1, 0.05)
        ..row()..center()
        ..add(img(Icons.bTNAbomination))
        ..row()..center()
        ..add(p("Abomination"))..padTop(0.01)

        new TableLayout(0.25, 0.35)
        ..row()
        ..add(h1("h1"))
        ..row()
        ..add(h2("h2"))
        ..row()
        ..add(h3("h3"))
        ..row()
        ..add(h4("h4"))
        ..row()
        ..add(h5("h5"))
        ..row()
        ..add(p("p1"))
        ..row()
        ..add(p2("p2"))
        ..row()
        ..add(p3("p3"))
        ..row()
        ..add(img(Textures.auraRune6, 0.075, true))
        ..row()
        ..add(btn("button")..setWidth(0.125)..setHeight(0.025))
        ..row()
        ..add(imgBtn(Icons.bTNHumanBuild))
        ..row()
        ..add(checkbox.getFrame())
        ..row()
        ..add(bar1.create())
        ..row()
        ..add(nestedTable1.createContainer(baseFrame))..padRight(0.0025)
        ..add(nestedTable2.createContainer(baseFrame))
        ..applyTo(baseFrame)

        let mb = CreateMultiboard()
        ..setRowCount(1)
        ..setColumnCount(1)
        ..show()
        ..setTitle("Demo Board")

        attachToMultiboard(mb, "DemoMb", 0.22, 0.08, true,
            (uiParent, anchor) -> begin
                let layout = new TableLayout(0.22, 0.1, "DemoMbLayout")
                layout.padding = padding(0.006, 0.012, 0.006, 0.012)

                layout
                ..row()
                ..add(p("Hello world"))
                ..row()
                ..add(img(Icons.bTNAcidBomb))..center()
                ..row()
                ..add(btn("Click me")..setWidth(0.1))..growX()
                layout.applyTo(anchor)
                return anchor
            end
        )


